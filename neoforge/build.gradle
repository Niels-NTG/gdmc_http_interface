plugins {
    // Note: This is only needed for multi-loader projects
    // The main architectury loom version is set at the start of the root build.gradle
    id "architectury-plugin" version "3.4-SNAPSHOT"
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_21

architectury {
    platformSetupLoomIde()
    neoForge()
}

loom {
    silentMojangMappingsLicense() // Shut the licencing warning

    neoForge {

    }
    mixin {

    }

    // "runs" isn't required, but when we do need it then it can be useful
    runs {
        client {
            client()
            setConfigName("NeoForge Client")
            ideConfigGenerated(false) // When true a run configuration file will be generated for IDE's. By default only set to true for the root project.
            runDir("../run")
            programArgs("--username", "Dev")
        }
        server {
            server()
            setConfigName("NeoForge Server")
            ideConfigGenerated(false)
            runDir("../run")
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
    mappings loom.layered()
    {
        officialMojangMappings()
    }

    // Neoforge
    neoForge "net.neoforged:neoforge:${rootProject.neoforge_version}"
}

tasks.register('deleteResources', Delete) {
    delete file("build/resources/main")
}

tasks.register('copyAllResources') {
    dependsOn(copyCommonResources)
}

processResources {
    dependsOn(tasks.named('copyAllResources'))
}

tasks.named('runClient') {
    dependsOn(tasks.named('copyAllResources'))
    finalizedBy(deleteResources)
}

remapJar {
    inputFile = shadowJar.archiveFile
    dependsOn shadowJar
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}
